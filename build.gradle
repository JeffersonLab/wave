plugins {
    id 'java'
	id 'war'
    id "org.gradlewebtools.minify" version "1.3.1"
    id "org.jlab.cat" version "1.0.0"
}

description = "Web Archive Viewer and Expositor"
group = 'org.jlab'
version = '2.1.0'

ext {
    releaseDate = 'May 21 2020'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.release = 8
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    providedCompile 'javax.servlet:jstl:1.2',
                    'javax.json:javax.json-api:1.1.4',
	                'javax.servlet:javax.servlet-api:3.1.0'
}

task combineJs(type: org.jlab.cat.CatTask) {
    input.from("src/main/webapp/resources/js/src").include('**/*.js')
    output = file("${buildDir}/combined-js/combined.js")
}

task minifyJs(type: org.gradlewebtools.minify.JsMinifyTask) {
    dependsOn(combineJs)
    srcDir = project.file("build/combined-js")
    dstDir = project.file("build/minified")
}

war {
    archiveFileName =  'wave.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            line -> line.replaceAll("@PRODUCTION_RELEASE@", "true")
        }
    }

    from("${buildDir}/minified") {
        include 'combined.min.js'
        into 'resources/js'
    }

    dependsOn minifyJs
}