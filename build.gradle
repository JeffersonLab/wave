plugins {
    id 'java'
	id 'war'
    id "org.gradlewebtools.minify" version "2.1.1"
    id 'com.diffplug.spotless' version '7.2.1'
    id "org.jlab.cat" version "1.1.0"
}

description = "Web Archive Viewer and Expositor"
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.version = project.version
ext.releaseDate = new Date().format('MMM dd yyyy')

repositories {
    mavenCentral()
}
tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
dependencies {
    implementation  'org.glassfish.web:jakarta.servlet.jsp.jstl:2.0.0'
    providedCompile 'javax.json:javax.json-api:1.1.4',
                    'jakarta.servlet:jakarta.servlet-api:6.0.0'
}
tasks.register('combineJs', org.jlab.cat.CatTask) {
    from("src/main/webapp/resources/js/src").include('**/*.js')
    output = file("${buildDir}/combined-js/combined.js")
}
tasks.register('minifyJs', org.gradlewebtools.minify.JsMinifyTask) {
    dependsOn(combineJs)
    srcDir = project.file("build/combined-js")
    dstDir = project.file("build/minified")
}
tasks.named('jar') {
    enabled = false
}
war {
    archiveFileName =  'wave.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            line -> line.replaceAll("@PRODUCTION_RELEASE@", "true")
        }
    }

    from("${buildDir}/minified") {
        include 'combined.min.js'
        into 'resources/js'
    }

    dependsOn minifyJs
}
spotless {
    java {
        googleJavaFormat()
    }
}