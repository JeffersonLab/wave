plugins {
    id 'java'
	id 'war'
    id "org.gradlewebtools.minify" version "1.3.1"
    id "org.jlab.cat" version "1.1.0"
}

description = "Web Archive Viewer and Expositor"
group = 'org.jlab'
version = '2.4.1'

ext {
    releaseDate = 'Jan 31 2023'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation  'org.glassfish.web:jakarta.servlet.jsp.jstl:2.0.0'
    providedCompile 'javax.json:javax.json-api:1.1.4',
                    'jakarta.servlet:jakarta.servlet-api:6.0.0'
}

task combineJs(type: org.jlab.cat.CatTask) {
    from("src/main/webapp/resources/js/src").include('**/*.js')
    output = file("${buildDir}/combined-js/combined.js")
}

task minifyJs(type: org.gradlewebtools.minify.JsMinifyTask) {
    dependsOn(combineJs)
    srcDir = project.file("build/combined-js")
    dstDir = project.file("build/minified")
}

war {
    archiveFileName =  'wave.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            line -> line.replaceAll("@PRODUCTION_RELEASE@", "true")
        }
    }

    from("${buildDir}/minified") {
        include 'combined.min.js'
        into 'resources/js'
    }

    dependsOn minifyJs
}